module NBE where

open import Data.Unit using (tt)
open import Data.Product using (_Ã—_ ; _,_ ; projâ‚ ; projâ‚‚)
open import Data.Sum using (_âŠ_ ; injâ‚ ; injâ‚‚)

open import Type
open import Presheaf
open ğ’« renaming (F to In ; fmap to wk)
open import Lambda renaming (_,_ to _`,_)

variable
  Î“ Î” : Env
  a b c : Ty
  A B : ğ’«

mutual

  {-
    Neutral terms are "blocked" terms in normal form i.e.,
    neutral terms are variables or eliminators which cannot be reduced further
    For example,
      - Î“ âŠ¢ (var zero) is blocked
      - Î“ âŠ¢ (app (var zero) unit) is blocked
    Neutral terms can be safely substituted for a variable
    without creating a possibility of further reduction.
  -}
  data Ne (Î“ : Env) : Ty â†’ Set where
    var : Var Î“ a      â†’ Ne Î“ a
    app : Ne Î“ (a â‡’ b) â†’ Nf Î“ a â†’ Ne Î“ b
    fst : Ne Î“ (a * b) â†’ Ne Î“ a
    snd : Ne Î“ (a * b) â†’ Ne Î“ b

  -- Normal forms are terms which cannot be reduced further
  data Nf (Î“ : Env) : Ty â†’ Set where
    abs  : Nf (Î“ `, a) b â†’ Nf Î“ (a â‡’ b)
    pair : Nf Î“ a â†’ Nf Î“ b â†’ Nf Î“ (a * b)
    -- TODO description of these promoters
    lift : Ne Î“ ğ•“ â†’  Nf Î“ ğ•“
    unit : Nf Î“ ğŸ™
    inl  : Nf Î“ a â†’ Nf Î“ (a + b)
    inr  : Nf Î“ b â†’ Nf Î“ (a + b)
    case : Ne Î“ (a + b) â†’ Nf (Î“ `, a) c â†’ Nf (Î“ `, b) c â†’ Nf Î“ c

  {-
    A note on `case` being in Nf:
    - Even though a case is an eliminator (of a coproduct),
      note that it ends up in normal form rather than neutral.
      This is because we do not want a nested case expression
      in the first argument (had they been in neutral, this would
      have been possible!), and instead be pushed into its branches
      (possibly to trigger further reductions), and this is precisely what the
      current definition allows.

    - because of case being here, normal forms are not unique due
      to the case ordering problem:
      for example,
        case ğŸ˜ (case ğŸ™ nâ‚ nâ‚‚) (case ğŸ™ mâ‚ mâ‚‚) â‰ˆ
        case ğŸ™ (case ğŸ˜ nâ‚ mâ‚) (case ğŸ˜ nâ‚‚ mâ‚‚)
      where and nâ‚,nâ‚‚,mâ‚,mâ‚‚ are distinct normal forms
      and â‰ˆ is the Î²Î·-equivalence
  -}

open import OPE

-- variable embedding
wkVar : Î” â‰¤ Î“ â†’ Var Î“ a â†’  Var Î” a
wkVar id x              = x
wkVar (weak e) x        = succ (wkVar e x)
wkVar (lift e) zero     = zero
wkVar (lift e) (succ x) = succ (wkVar e x)

-- term embedding
wkTm : Î” â‰¤ Î“ â†’ Tm Î“ a  â†’ Tm Î” a
wkTm e (var x)        = var (wkVar e x)
wkTm e (abs t)        = abs (wkTm (lift e) t)
wkTm e (app f x)      = app (wkTm e f) (wkTm e x)
wkTm e (pair x y)     = pair (wkTm e x) (wkTm e y)
wkTm e unit           = unit
wkTm e (fst t)        = fst (wkTm e t)
wkTm e (snd t)        = snd (wkTm e t)
wkTm e (inl t)        = inl (wkTm e t)
wkTm e (inr t)        = inr (wkTm e t)
wkTm e (case x f g)   = case (wkTm e x) (wkTm (lift e) f) (wkTm (lift e) g)

private

  -- as an EXAMPLE of need for term embedding, consider eta expansion
  eta : Tm Î“ (a â‡’ b) â†’ Tm Î“ (a â‡’ b)
  eta f = abs (app (wkTm (weak id) f) (var zero))

mutual

  -- weaken a normal form
  wkNf : Î” â‰¤ Î“ â†’ Nf Î“ a  â†’ Nf Î” a
  wkNf e (abs n)      = abs (wkNf (lift e) n)
  wkNf e (pair p q)   = pair (wkNf e p) (wkNf e q)
  wkNf e unit         = unit
  wkNf e (inl x)      = inl (wkNf e x)
  wkNf e (inr x)      = inr (wkNf e x)
  wkNf e (case x p q) = case (wkNe e x) (wkNf (lift e) p) (wkNf (lift e) q)
  wkNf e (lift x)     = lift (wkNe e x)

  -- weaken a neutral form
  wkNe : Î” â‰¤ Î“ â†’ Ne Î“ a  â†’ Ne Î” a
  wkNe e (var x)   = var (wkVar e x)
  wkNe e (app n x) = app (wkNe e n) (wkNf e x)
  wkNe e (fst x)   = fst (wkNe e x)
  wkNe e (snd x)   = snd (wkNe e x)

-- presheafs indexed by a type
module IndexedPresheafs where

  Var' : (a : Ty) â†’ ğ’«
  Var' a .In Î“ = Var Î“ a
  Var' a .wk   = wkVar

  Ne' : (a : Ty) â†’ ğ’«
  Ne' a .In Î“ = Ne Î“ a
  Ne' a .wk   = wkNe

  Tm' : (a : Ty) â†’ ğ’«
  Tm' a .In Î“ = Tm Î“ a
  Tm' a .wk   = wkTm

  Nf' : (a : Ty) â†’ ğ’«
  Nf' a .In Î“ = Nf Î“ a
  Nf' a .wk   = wkNf

open IndexedPresheafs

module DecMonad where

  data Dec (Î“ : Env) (A : ğ’«) : Set where
    leaf  : (a : In A Î“) â†’  Dec Î“ A
    branch : âˆ€{C D} â†’ Ne Î“ (C + D) â†’ Dec (Î“ `, C) A â†’  Dec (Î“ `, D) A â†’ Dec Î“ A

  wkDec : Î” â‰¤ Î“ â†’ Dec Î“ A  â†’ Dec Î” A
  wkDec {A = A} e (leaf a)      = leaf (A .wk e a)
  wkDec {A = A} e (branch x p q) =
    branch ((Ne' _) .wk e x)
      (wkDec (lift e) p)
      (wkDec (lift e) q)

  -- Dec' is an endofunctor on category of presheaves (construction and properties below)

  -- object map of Dec'
  Dec' : (A : ğ’«) â†’ ğ’«
  Dec' A .In Î“ = Dec Î“ A
  Dec' A .wk   = wkDec

  -- morphism map of Dec'
  mapDec : (A â†’Ì‡ B) â†’ (Dec' A â†’Ì‡ Dec' B)
  mapDec t (leaf a) = leaf (t a)
  mapDec t (branch x p q) = branch x (mapDec t p) (mapDec t q)

  -- Dec' is an "idempotent" functor
  joinDec : Dec' (Dec' A) â†’Ì‡ Dec' A
  joinDec (leaf a) = a
  joinDec (branch x p q) = branch x (joinDec p) (joinDec q)

  cojoinDec : Dec' A â†’Ì‡ Dec' (Dec' A)
  cojoinDec = leaf

  -- a morphism from an exponential to an exponential of it's covered components
  -- i.e., (b ^ a) â†’Ì‡ (Dec' b) ^ (Dec' a)
  expDec :  (A â‡’' B) â†’Ì‡ (Dec' A â‡’' Dec' B)
  expDec f Ï„ (leaf a) = leaf (f Ï„ a)
  expDec f Ï„ (branch x câ‚ câ‚‚) = branch x (expDec f (weak Ï„) câ‚) (expDec f (weak Ï„) câ‚‚)

open DecMonad

-- assign semantics to types and environments using presheafs
module PresheafSemantics where

  âŸ¦_âŸ§ : Ty â†’ ğ’«
  âŸ¦   ğŸ™   âŸ§ = ğŸ™'
  âŸ¦ A â‡’ B âŸ§ = âŸ¦ A âŸ§ â‡’' âŸ¦ B âŸ§
  âŸ¦ A * B âŸ§ = âŸ¦ A âŸ§ Ã—' âŸ¦ B âŸ§
  âŸ¦ A + B âŸ§ = Dec' (âŸ¦ A âŸ§ +' âŸ¦ B âŸ§)
  âŸ¦   ğ•“   âŸ§ = Nf' ğ•“

  âŸ¦_âŸ§â‚‘ : Env â†’ ğ’«
  âŸ¦ [] âŸ§â‚‘ = ğŸ™'
  âŸ¦ e `, a âŸ§â‚‘ = âŸ¦ e âŸ§â‚‘ Ã—' âŸ¦ a âŸ§

open PresheafSemantics

-- special cover operations
module DecOps where

  -- special case of expDec
  mapDec' : In (A â‡’' B) Î” â†’ Dec Î” A â†’ Dec Î” B
  mapDec' f c = expDec f id c

  -- convert decision over normal forms to a normal form
  convertNf : Dec' (Nf' a) â†’Ì‡ Nf' a
  convertNf (leaf a)       = a
  convertNf (branch x p q) = case x (convertNf p) (convertNf q)

  -- make decision over a semantic value
  mkDec : Dec' âŸ¦ a âŸ§ â†’Ì‡ âŸ¦ a âŸ§
  mkDec {ğŸ™}     c         = tt
  mkDec {a * b} c         = mkDec {a} (mapDec projâ‚ c) , mkDec {b} (mapDec projâ‚‚ c)
  mkDec {a + b} c         = joinDec c
  mkDec {ğ•“}     c         = convertNf c
  mkDec {a â‡’ b} f {Î”} e x = mkDec {b} y
    where
    f' : Dec Î” âŸ¦ a â‡’ b âŸ§
    f' = wkDec e f
    y : Dec Î” âŸ¦ b âŸ§
    y = mapDec' (Î» e' g â†’ (g id (âŸ¦ a âŸ§ .wk e' x))) f'

open DecOps

-----  THE MEAT!

mutual

  reflect : Ne' a â†’Ì‡ âŸ¦ a âŸ§
  reflect {ğŸ™} _     = tt
  reflect {ğ•“} b     = lift b
  reflect {a â‡’ b} f = Î» Ï„ â†’
    let f' = (wkNe Ï„ f)
    in Î» x â†’ reflect (app f' (reify x))
  reflect {A * B} t = reflect (fst t) , reflect (snd t)
  reflect {A + B} t =
    branch t
      (leaf (injâ‚ (reflect {A} (var zero))))
      (leaf (injâ‚‚ (reflect {B} (var zero))))

  reify : âŸ¦ a âŸ§ â†’Ì‡  Nf' a
  reify {ğŸ™} tt           = unit
  reify {ğ•“} a            = a
  reify {A â‡’ B} f        = abs (reify (f fresh (reflect {A} (var zero))))
  reify {A * B} (P , Q)  = pair (reify P) (reify Q)
  reify {A + B} t        = convertNf (mapDec reifyOr t)
    where
      reifyOr : (âŸ¦ a âŸ§ +' âŸ¦ b âŸ§) â†’Ì‡ Nf' (a + b)
      reifyOr (injâ‚ x) = inl (reify x)
      reifyOr (injâ‚‚ y) = inr (reify y)

-- perform a lookup in the meta language
-- using an index in the object language
lookup : Var Î“ a â†’ (âŸ¦ Î“ âŸ§â‚‘ â†’Ì‡  âŸ¦ a âŸ§)
lookup zero (Î“ , a)     = a
lookup (succ v) (Î“ , _) = lookup v Î“

-- interpret a Tm in the meta theory (in Set)
-- i.e., denotational semantics for (possibly open) Tms
eval : Tm Î“ a â†’ âŸ¦ Î“ âŸ§â‚‘ â†’Ì‡ âŸ¦ a âŸ§
eval (var x) Î³         = lookup x Î³
eval {Î“} (abs f) Î³ = Î» Ï„ x â†’ eval f (âŸ¦ Î“ âŸ§â‚‘ .wk Ï„ Î³ , x)
eval (app f x) Î³       = eval f Î³ id (eval x Î³)
eval (pair p q) Î³      = (eval p Î³) , (eval q Î³)
eval unit Î³            = tt
eval (fst x) Î³         = projâ‚ (eval x Î³)
eval (snd x) Î³         = projâ‚‚ (eval x Î³)
eval (inl x) Î³         = leaf (injâ‚ (eval x Î³))
eval (inr x) Î³         = leaf (injâ‚‚ (eval x Î³))
eval {Î“} {c} (case {a} {b} x p q) {Î”} Î³ =
  mkDec {c} (mapDec' match (eval x Î³))
  where
    match : In ((âŸ¦ a âŸ§ +' âŸ¦ b âŸ§) â‡’' âŸ¦ c âŸ§) Î”
    match Ï„ (injâ‚ x) = eval p (âŸ¦ Î“ âŸ§â‚‘ .wk Ï„ Î³ , x)
    match Ï„ (injâ‚‚ y) = eval q (âŸ¦ Î“ âŸ§â‚‘ .wk Ï„ Î³ , y)

-- semantics of the identity environment
reflect_env : âˆ€ (Î“ : Env) â†’ âŸ¦ Î“ âŸ§â‚‘ .In Î“
reflect_env []       = tt
reflect_env (Î“ `, a) =  env' , reflect {a} (var zero)
  where
  -- we weaken the semantics (env Î“)
  -- using the the weakener in the presheaf âŸ¦ Î“ âŸ§â‚‘
  env' = âŸ¦ Î“ âŸ§â‚‘ .wk fresh (reflect_env Î“)

-- eval lifts a term to an agda term
-- and reify forces evaluation of its argument (the agda term) and uses the
-- result to produce a normal form
norm : Tm' a â†’Ì‡ Nf' a
norm {_} {Î“} t = reify (eval t (reflect_env Î“))

-- See, I told you! ' â†’Ì‡ ' is just a way make (Î“ : Env) implicit
normalize : Tm Î“ a â†’ Nf Î“ a
normalize = norm
